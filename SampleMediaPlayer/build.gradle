// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0-alpha3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    group = 'com.vmn.android'
    ext {
        def nexusPropsFile = rootProject.file('secure/nexus.properties')
        LDAP_USERNAME = getProperty('LDAP_USERNAME', nexusPropsFile)
        LDAP_PASSWORD = getProperty('LDAP_PASSWORD', nexusPropsFile)
    }
    repositories {
        mavenCentral()
        jcenter()
        maven {
            credentials {
                username LDAP_USERNAME
                password LDAP_PASSWORD
            }
            url "http://nexus.mtvi.com/nexus/content/repositories/releases"
        }
        maven {
            credentials {
                username LDAP_USERNAME
                password LDAP_PASSWORD
            }
            url "http://nexus.mtvi.com/nexus/content/repositories/thirdparty"
        }
    }

}

static def getProperty(String prop, File propsFile) {
    if (propsFile.exists()) {
        //if the user has a properties file, get the username and pass from there
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[prop]
    } else {
        //if not, try the environment variables (for ci / build servers)
        return System.getenv(prop)
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
